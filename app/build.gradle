plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def runTasks = gradle.startParameter.taskNames
// Gradle uses "assembleRelease, and Android Studio uses :schoologyApp:assembleRelease
def isReleaseTask = 'assembleRelease' in runTasks || ':GitLabCICD:assembleRelease' in runTasks

android {
    namespace 'com.murali.gitlabcicd'
    compileSdk 33

    lintOptions {
        // Disable MissingTranslations for Debug/RC builds.
        if (!isReleaseTask) {
            disable 'MissingTranslation'
        }

        //we use kotlin Parcelize which creates Parcel at compile time, so it's fine to disable
        //ParcelCreator
        //DuplicatePlatformClasses should be fixed by updating our dependencies
        disable 'GradleDependency', 'ParcelCreator', 'DuplicatePlatformClasses', 'ObsoleteLintCustomCheck'
        abortOnError isReleaseTask
    }

    dexOptions {
        javaMaxHeapSize "8192m"
    }

    defaultConfig {
        applicationId "com.murali.gitlabcicd"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            File propsFile = file('signing.properties')
            if (propsFile.exists() && propsFile.canRead()) {
                Properties props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file('SchoologyAppAndroid.keystore')
                storePassword = props['STORE_PASSWORD']
                keyAlias = props['KEY_ALIAS']
                keyPassword = props['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard-project.txt'
            signingConfig signingConfigs.release
        }

        rc { // release candidate build (for testing)
            minifyEnabled true
            proguardFiles 'proguard-debug-project.txt'
            signingConfig signingConfigs.release
            matchingFallbacks = ['debug', 'release']
        }

        debug {
            minifyEnabled false
            proguardFile 'proguard-debug-project.txt'
            testProguardFiles 'proguard-debug-project.txt'
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            reports {
                html.enabled = true
                junitXml.setDestination(file("$buildDir/test-results/debug/"))
                html.setDestination(file("$buildDir/reports/tests"))
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            exclude 'META-INF/DEPENDENCIES'
            exclude "org/codehaus/jackson/impl/VERSION.txt"
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}